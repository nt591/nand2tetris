// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // if sel is 1, choose empty bus, else x
    Mux16(a=x, b[0..15]=false, sel=zx, out=nxmux);
    // temp toggle
    Not16(in=nxmux, out=zxnot);
    // if nx is 1, choose toggle, else zxa
    Mux16(a=nxmux, b=zxnot, sel=nx, out=nxa);

    // same for y
    Mux16(a=y, b[0..15]=false, sel=zy, out=nymux);
    // temp toggle
    Not16(in=nymux, out=zynot);
    // if nx is 1, choose toggle, else zxa
    Mux16(a=nymux, b=zynot, sel=ny, out=nya);

    // temp additions and ANDs
    And16(a=nxa, b=nya, out=xyAnd);
    Add16(a=nxa, b=nya, out=xyAdd);

    // mux16 on f
    Mux16(a=xyAnd, b=xyAdd, sel=f, out=fSelect);

    // negate if no
    Not16(in=fSelect, out=fNegate);
    Mux16(a=fSelect, b=fNegate, sel=no, out=out,
        out[15]=ng, out[0..7]=tmp1, out[8..15]=tmp2
    );

    // set ZR
    // OR all the values in output and see if any are 1
    Or8Way(in=tmp1, out=zr1);
    Or8Way(in=tmp2, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}
